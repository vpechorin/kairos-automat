buildscript {
    ext {
        log4jVersion = '2.11.1'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
    id "pl.allegro.tech.build.axion-release" version "1.10.0"
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

group = 'net.pechorina.kairos.automat'
scmVersion {
    tag {
        prefix = 'kairo-automat'
    }
}

project.version = scmVersion.version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.12.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
    testRuntime("org.junit.platform:junit-platform-launcher:1.2.0")
    testRuntime("org.junit.vintage:junit-vintage-engine:5.2.0")
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.2.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'io.github.microutils:kotlin-logging:1.6.25'
}

test {

    useJUnitPlatform {
        includeEngines 'junit-jupiter'
    }

    systemProperty 'log4j2.debug', 'false'

    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
        showStandardStreams = true
    }

    minHeapSize = "48m"
    maxHeapSize = "96m"
    testLogging.exceptionFormat "full"

    ignoreFailures false
    reports.html.enabled = true

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

def stUser = hasProperty('sonatypeUsername') ? sonatypeUsername : "anonymous"
def stPass = hasProperty('sonatypePassword') ? sonatypePassword : "secret"

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'Kairos Automat'
                description = 'Kairos Automat is a finite-state machine library'
                url = 'https://github.com/vpechorin/kairos-automat'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'vpechorin'
                        name = 'Victor Pechorin'
                        email = 'dev@pechorina.net'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:vpechorin/kairos-automat.git'
                    url = 'https://github.com/vpechorin/kairos-automat'
                }
            }
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username stUser
                password stPass
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
